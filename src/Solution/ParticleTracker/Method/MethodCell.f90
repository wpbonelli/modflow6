module MethodCellModule

  use KindModule, only: DP, I4B, LGP
  use ErrorUtilModule, only: pstop
  use ConstantsModule, only: DONE, DZERO
  use MethodModule, only: MethodType
  use ParticleModule, only: ParticleType, TERM_NO_EXITS, TERM_BOUNDARY
  use ParticleEventModule, only: ParticleEventType, CellExitEventType
  use CellDefnModule, only: CellDefnType
  use IteratorModule, only: IteratorType
  implicit none

  private
  public :: MethodCellType

  type, abstract, extends(MethodType) :: MethodCellType
  contains
    procedure, public :: assess
    procedure, public :: cellexit
    procedure, public :: check_cycle
    procedure, public :: store_event
  end type MethodCellType

contains

  !> @brief Check reporting/terminating conditions before tracking
  !! the particle across the cell.
  !!
  !! Check a number of conditions determining whether to continue
  !! tracking the particle or terminate it, as well as whether to
  !! record any output data as per selected reporting conditions.
  !<
  subroutine assess(this, particle, cell_defn, tmax)
    ! modules
    use TdisModule, only: endofsimulation, totimc, totim
    use ParticleModule, only: TERM_WEAKSINK, TERM_NO_EXITS, &
                              TERM_STOPZONE, TERM_INACTIVE
    use ParticleEventModule, only: CELLEXIT, TERMINATE, &
                                   TIMESTEP, WEAKSINK, USERTIME
    ! dummy
    class(MethodCellType), intent(inout) :: this
    type(ParticleType), pointer, intent(inout) :: particle
    type(CellDefnType), pointer, intent(inout) :: cell_defn
    real(DP), intent(in) :: tmax
    ! local
    logical(LGP) :: dry_cell, dry_particle, no_exit_face, stop_zone, weak_sink
    integer(I4B) :: i
    real(DP) :: t, ttrackmax

    dry_cell = this%fmi%ibdgwfsat0(cell_defn%icell) == 0
    dry_particle = particle%z > cell_defn%top
    no_exit_face = cell_defn%inoexitface > 0
    stop_zone = cell_defn%izone > 0 .and. particle%istopzone == cell_defn%izone
    weak_sink = cell_defn%iweaksink > 0

    particle%izone = cell_defn%izone
    if (stop_zone) then
      call this%terminate(particle, status=TERM_STOPZONE)
      return
    end if

    if (no_exit_face .and. .not. dry_cell) then
      call this%terminate(particle, status=TERM_NO_EXITS)
      return
    end if

    if (weak_sink) then
      if (particle%istopweaksink > 0) then
        call this%terminate(particle, status=TERM_WEAKSINK)
        return
      else
        call this%weaksink(particle)
      end if
    end if

    if (dry_cell) then
      if (particle%idrymeth == 0) then
        ! drop to cell bottom. handled by pass
        ! to bottom method, nothing to do here
        no_exit_face = .false.
      else if (particle%idrymeth == 1) then
        ! stop
        call this%terminate(particle, status=TERM_INACTIVE)
        return
      else if (particle%idrymeth == 2) then
        ! stay
        particle%advancing = .false.
        no_exit_face = .false.

        ! we might report tracking times
        ! out of order here, but we want
        ! the particle termination event
        ! (if this is the last time step)
        ! to have the maximum tracking t,
        ! so we need to keep tabs on it.
        ttrackmax = totim

        ! update tracking time to time
        ! step end time and save record
        particle%ttrack = totim
        call this%timestep(particle)

        ! record user tracking times
        call this%tracktimes%advance()
        if (this%tracktimes%any()) then
          do i = this%tracktimes%selection(1), this%tracktimes%selection(2)
            t = this%tracktimes%times(i)
            if (t < totimc) cycle
            if (t >= tmax) exit
            particle%ttrack = t
            call this%usertime(particle)
            if (t > ttrackmax) ttrackmax = t
          end do
        end if

        ! terminate if last period/step
        if (endofsimulation) then
          particle%ttrack = ttrackmax
          call this%terminate(particle, status=TERM_NO_EXITS)
          return
        end if
      end if
    else if (dry_particle .and. this%name /= "passtobottom") then
      if (particle%idrymeth == 0) then
        ! drop to water table
        particle%z = cell_defn%top
      else if (particle%idrymeth == 1) then
        ! stop
        call this%terminate(particle, status=TERM_INACTIVE)
        return
      else if (particle%idrymeth == 2) then
        ! stay
        particle%advancing = .false.
        no_exit_face = .false.

        ! we might report tracking times
        ! out of order here, but we want
        ! the particle termination event
        ! (if this is the last time step)
        ! to have the maximum tracking t,
        ! so we need to keep tabs on it.
        ttrackmax = totim

        ! update tracking time to time
        ! step end time and save record
        particle%ttrack = totim
        call this%timestep(particle)

        ! record user tracking times
        call this%tracktimes%advance()
        if (this%tracktimes%any()) then
          do i = this%tracktimes%selection(1), this%tracktimes%selection(2)
            t = this%tracktimes%times(i)
            if (t < totimc) cycle
            if (t >= tmax) exit
            particle%ttrack = t
            call this%usertime(particle)
            if (t > ttrackmax) ttrackmax = t
          end do
        end if
      end if
    end if

    if (no_exit_face) then
      particle%advancing = .false.
      particle%istatus = TERM_NO_EXITS
      call this%terminate(particle)
      return
    end if

  end subroutine assess

  !> @brief Particle exits a cell.
  subroutine cellexit(this, particle)
    class(MethodCellType), intent(inout) :: this
    type(ParticleType), pointer, intent(inout) :: particle
    class(ParticleEventType), pointer :: event

    allocate (CellExitEventType :: event)
    select type (event)
    type is (CellExitEventType)
      event%exit_face = particle%iboundary(2)
    end select
    call this%events%dispatch(particle, event)
    call this%check_cycle(particle, event)
    call this%store_event(particle, event)
  end subroutine cellexit

  !> @brief Check for cycles in the particle's path.
  subroutine check_cycle(this, particle, event)
    ! dummy
    class(MethodCellType), intent(inout) :: this
    type(ParticleType), pointer, intent(inout) :: particle !< particle
    class(ParticleEventType), pointer, intent(in) :: event !< particle event to check for
    ! local
    class(IteratorType), allocatable :: itr
    class(ParticleEventType), pointer :: evt_ptr
    logical(LGP) :: found_cycle, found_boundary
    integer(I4B) :: i_back

    found_cycle = .false.
    found_boundary = .false.
    select type (event)
    type is (CellExitEventType)
      itr = particle%history%Iterator()
      do while (itr%has_next())
        call itr%next()
        select type (prev => itr%value())
        class is (CellExitEventType)
          if (event%icu == prev%icu .and. &
              event%ilay == prev%ilay .and. &
              event%izone == prev%izone .and. &
              event%exit_face == prev%exit_face .and. &
              event%exit_face /= 0) then
            if (event%exit_face == 7) then
              ! the exit face is 7, we have a boundary exit in
              ! a well and we should not consider this a cycle
              found_boundary = .true.
            else
              found_cycle = .true.
            end if
            exit
          end if
        end select
      end do
    end select
    if (found_boundary) return
    if (found_cycle) then
      print *, "Detected duplicate cell exit events:"
      i_back = particle%history%Count() - 1
      itr = particle%history%Iterator()
      do while (itr%has_next())
        call itr%next()
        select type (e => itr%value())
        class is (ParticleEventType)
          evt_ptr => e
          print *, i_back, "ago:"
          call evt_ptr%log(0)
          i_back = i_back - 1
        end select
      end do
      print *, "Current:"
      call event%log(0)
      call this%terminate(particle, status=TERM_NO_EXITS)
    end if
  end subroutine check_cycle

  !> @brief Store the cell exit event in the particle's history.
  subroutine store_event(this, particle, event)
    ! dummy
    class(MethodCellType), intent(inout) :: this
    type(ParticleType), pointer, intent(inout) :: particle
    class(ParticleEventType), pointer, intent(in) :: event
    ! local
    class(*), pointer :: p

    select type (event)
    type is (CellExitEventType)
      p => event
      call particle%history%Add(p)
      if (particle%history%Count() > particle%icycwin) &
        call particle%history%RemoveNode(1, .true.)
    end select
  end subroutine store_event

end module MethodCellModule
